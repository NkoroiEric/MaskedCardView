import org.gradle.internal.os.OperatingSystem
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
    id 'maven-publish'
}

version = libraryVersion

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    androidExtensions {
        experimental =  true
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from(android.sourceSets.main.java.srcDirs) {
        // Needed because we have Java sources and resources in same directory
        include '**/*.kt'
        includeEmptyDirs = false
    }
}
afterEvaluate {
    publishing {
        repositories {
            maven {
                url = "/tmp/MyRepo"
            }
        }

        publications {
            release(MavenPublication) {
                from components.release

                artifact androidSourcesJar

                groupId = 'com.enterprise.lib'
                artifactId = 'material'
                version project.version
                pom {
                    name = "Material MaskedCardView"
                    description = "Material MaskedCardView Supports Edges And Horizontal Corners" +
                            " Compatible on devices running API 14 or later."
                    url = 'http://some-url to github'
                    inceptionYear = '2020'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = 'The Android Enterprise'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/material-components/material-components-android.git'
                        url = 'https://github.com/material-components/material-components-android'
                    }

                }
            }
        }
    }
}